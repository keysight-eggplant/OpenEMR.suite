on CheckProviderCalendar
	//Confirm that calendar has finished loading
	--(875,252,1593,284)
	put ImageRectangle("OpenEMR_nextdatebutton") into myNextDateRectangle
	put ImageRectangle("OpenEMR_printschedulebutton") into myPrintScheduleRectagle
	put the right of myNextDateRectangle into x1
	put (the bottom of myNextDateRectangle) + 10 into y1
	put the left of myPrintScheduleRectagle into x2
	put (the bottom of myNextDateRectangle) + 42 into y2
	WaitFor 1.0, "OpenEMR_CloseProviderScheduleX"
	set mySchedulingProviderFirstLastName to "Elizabeth Corday"
	set mySchedulingProviderReadArea to [x1,y1,x2,y2]
	set mySchedulingProviderName to ReadText(mySchedulingProviderReadArea)
	If mySchedulingProviderName = mySchedulingProviderFirstLastName then
		LogSuccess "The correct provider schedule is displayed"
	else
		LogWarning "The correct provider schedule is NOT displayed"
	End If
end CheckProviderCalendar

on CheckLoginUserName UserName
	//Verify correct login user
	put ImageRectangle(Image:"OpenEMR_UserIcon", WaitFor:30) + (-120, 20, 0, 50) into myLoginUserReadArea
	Click FoundImageLocation()
	TypeText escapeKey
	set myLoginUserFirstLastName to UserName
	set myLoginUserName to ReadText(myLoginUserReadArea)
	If myLoginUserName contains myLoginUserFirstLastName then
		LogSuccess "You have successfully logged in"
	else
		LogWarning "You have NOT successfully logged in "
	End If
	Click (800,200)
end CheckLoginUserName
